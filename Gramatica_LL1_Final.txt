Terminales = { boolean else float function if int let read return string void write idnum intconst realconst str idbool idstr idfun pluseq eq comma semicolon oppar clpar opbra clbra sum and minorthan boolconst eof }

NoTerminales = { S AF AB A Tipo Id Igualar IgualarPre Descriptor DescriptorO DescriptorAux DescriptorParAux DescriptorFunAux NumRecur TermNum2 BoolRecur Cadena LlamarFuncion ArgPre ArgRec WriteAlt Cond ARec CondEl ElseAux DefFuncion DefFuncionAux Arg ArgAux }

Axioma = S

Producciones = {
S -> AF eof

AF -> AB AF
AF -> DefFuncion AF
AF -> lambda

AB -> A semicolon
AB -> if oppar Descriptor clpar Cond

A -> let Tipo Id Igualar
A -> Id IgualarPre
A -> LlamarFuncion
A -> read Id
A -> write WriteAlt

A -> lambda

Tipo -> int
Tipo -> float
Tipo -> string
Tipo -> boolean

Id -> idnum
Id -> idbool
Id -> idstr

IgualarPre -> Igualar
IgualarPre -> pluseq Descriptor

Igualar -> eq Descriptor
Igualar -> lambda

//// PRODUCCIONES CORREGIDAS PARA DESCRIPTOR ////

DescriptorO -> Descriptor
DescriptorO -> lambda

Descriptor -> idnum DescriptorAux
Descriptor -> intconst DescriptorAux
Descriptor -> realconst DescriptorAux
Descriptor -> idbool BoolRecur
Descriptor -> boolconst BoolRecur
Descriptor -> oppar DescriptorParAux
Descriptor -> idfun DescriptorFunAux
Descriptor -> idstr
Descriptor -> str

DescriptorAux -> sum TermNum2 NumRecur
DescriptorAux -> minorthan TermNum2 BoolRecur
DescriptorAux -> and Descriptor BoolRecur
DescriptorAux -> lambda

DescriptorParAux -> TermNum2 NumRecur clpar sum TermNum2 NumRecur
DescriptorParAux -> TermNum2 NumRecur clpar minorthan TermNum2 BoolRecur
DescriptorParAux -> TermNum2 NumRecur clpar and Descriptor BoolRecur
DescriptorParAux -> TermNum2 NumRecur clpar

DescriptorFunAux -> oppar ArgPre clpar sum TermNum2 NumRecur
DescriptorFunAux -> oppar ArgPre clpar minorthan TermNum2 BoolRecur
DescriptorFunAux -> oppar ArgPre clpar and Descriptor BoolRecur
DescriptorFunAux -> oppar ArgPre clpar

//// FIN PRODUCCIONES DESCRIPTOR ////

NumRecur -> sum TermNum2 NumRecur
NumRecur -> lambda

TermNum2 -> idnum
TermNum2 -> intconst
TermNum2 -> realconst
TermNum2 -> oppar TermNum2 NumRecur clpar
TermNum2 -> idfun oppar ArgPre clpar

BoolRecur -> and Descriptor BoolRecur
BoolRecur -> lambda

Cadena -> idstr
Cadena -> str

LlamarFuncion -> idfun oppar ArgPre clpar

ArgPre -> Descriptor ArgRec
ArgPre -> lambda

ArgRec -> comma Descriptor ArgRec
ArgRec -> lambda

WriteAlt -> Descriptor
WriteAlt -> oppar Descriptor ArgRec clpar

Cond -> AB CondEl
Cond -> opbra ARec clbra CondEl

ARec -> AB ARec
ARec -> lambda

CondEl -> else ElseAux
CondEl -> lambda

ElseAux -> AB
ElseAux -> opbra ARec clbra

DefFuncion -> function DefFuncionAux idfun oppar Arg clpar opbra ARec clbra

DefFuncionAux -> Tipo
DefFuncionAux -> void

Arg -> Tipo Id ArgAux
Arg -> lambda

ArgAux -> comma Tipo Id ArgAux
ArgAux -> lambda
}
